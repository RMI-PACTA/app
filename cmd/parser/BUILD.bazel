load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_library")
load("@rules_pkg//:pkg.bzl", "pkg_tar")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_push", "oci_tarball")

go_library(
    name = "parser_lib",
    srcs = ["main.go"],
    importpath = "github.com/RMI/pacta/cmd/parser",
    visibility = ["//visibility:private"],
    deps = [
        "//async",
        "//azure/azblob",
        "//azure/azcreds",
        "//azure/azlog",
        "//task",
        "@com_github_azure_azure_sdk_for_go_sdk_azidentity//:azidentity",
        "@com_github_azure_azure_sdk_for_go_sdk_messaging_azeventgrid//publisher",
        "@com_github_namsral_flag//:flag",
        "@org_uber_go_zap//:zap",
        "@org_uber_go_zap//zapcore",
        "@org_uber_go_zap_exp//zapfield",
    ],
)

go_binary(
    name = "parser",
    embed = [":parser_lib"],
    visibility = ["//visibility:public"],
)

pkg_tar(
    name = "parser_tar",
    srcs = [":parser"],
)

filegroup(
    name = "configs",
    srcs = glob(["configs/**"]),
    visibility = ["//visibility:public"],
)

pkg_tar(
    name = "configs_tar",
    srcs = [":configs"],
    package_dir = "/configs",
    strip_prefix = "/cmd/parser/configs",
)

oci_image(
    name = "image",
    base = "@parser_base",
    entrypoint = ["/parser"],
    tars = [
        ":parser_tar",
        ":configs_tar",
    ],
)

oci_push(
    name = "push_image",
    image = ":image",
    remote_tags = ["latest"],
    repository = "rmisa.azurecr.io/pactaparser",
)

# Note: This tarball is provided for local testing of the Docker image, see the README.md for details on usage.
oci_tarball(
    name = "image_tarball",
    image = ":image",
    repo_tags = [],
)
